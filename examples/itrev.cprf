Lemma itrev_rev: itrev xs ys .=. reverse xs ++ ys

Proof by induction on List xs

Case [] -- base case

    To show: itrev [] ys .=. reverse [] ++ ys

                            itrev [] ys
    (by def itrev)      .=. ys

    reverse [] ++ ys
    (by def reverse)    .=. [] ++ ys
    (by def ++)         .=. ys

Case :  -- induction step

    To show: itrev (x:xs) ys .=. reverse (x:xs) ++ ys
    IH 1: itrev xs ys .=. reverse xs ++ ys

                            itrev (x:xs) ys
    (by def itrev)      .=. itrev xs (x:ys)
    (by IH 1)           .=. reverse xs ++ (x:ys)

                            reverse (x:xs) ++ ys
    (by def reverse)    .=. (reverse xs ++ [x]) ++ ys
    (by app_assoc)      .=. reverse xs ++ ([x] ++ ys)
    (by def ++)         .=. reverse xs ++ (x : ([] ++ ys))
    (by def ++)         .=. reverse xs ++ (x:ys)

QED


Lemma app_Nil: xs ++ [] .=. xs

Proof by induction on List xs

Case []

    To show: [] ++ [] .=. []

                            [] ++ []
    (by def ++)         .=. []

Case :

    To show: (x:xs) ++ [] .=. (x:xs)
    IH: xs ++ [] .=. xs

                            (x:xs) ++ []
    (by def ++)         .=. x : (xs ++ [])
    (by IH)             .=. x : xs

QED


Lemma: itrev xs [] .=. reverse xs

Proof

                            itrev xs []
    (by itrev_rev)      .=. reverse xs ++ []
    (by app_Nil)        .=. reverse xs

QED
